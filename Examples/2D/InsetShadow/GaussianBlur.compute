struct PushConstant
{
    int radius;
};

[[vk::push_constant]] ConstantBuffer<PushConstant> Param : register(b0, space0);
[[vk::binding(0, 0)]] StructuredBuffer<float> Kernel : register(t0, space0);
[[vk::binding(1, 0)]] Texture2D<float4> InputTexture : register(t1);
[[vk::binding(2, 0)]] RWTexture2D<float4> OutputTexture : register(u0);

[numthreads(16, 16, 1)]
void main(uint3 dispatchThreadID : SV_DispatchThreadID)
{
    uint2 outputTextureSize;
    OutputTexture.GetDimensions(outputTextureSize.x, outputTextureSize.y);
    
    uint2 coord = dispatchThreadID.xy;
    if ((coord.x >= outputTextureSize.x) || (coord.y >= outputTextureSize.y))
        return;
    
    uint2 inputTextureSize;
    InputTexture.GetDimensions(inputTextureSize.x, inputTextureSize.y);
    
    float4 sum = float4(0.0, 0.0, 0.0, 0.0);
    for (int dy = -Param.radius; dy <= Param.radius; dy++)
        for (int dx = -Param.radius; dx <= Param.radius; dx++)
        {
            int2 coordClamp = clamp(int2(coord) + int2(dx, dy), int2(0, 0), inputTextureSize - int2(1, 1));
            float4 color = InputTexture.Load(int3(coordClamp, 0));
            float weight = Kernel[dx + Param.radius] * Kernel[dy + Param.radius];
            sum += color * weight;
        }
    OutputTexture[coord] = sum;
}
